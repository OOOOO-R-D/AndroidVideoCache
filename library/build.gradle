repositories {
    jcenter()
    google()
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "maven-publish"

def githubProperties = new Properties()
try {
    githubProperties.load(new FileInputStream(rootProject.file("local.properties")))
} catch(exception) {

}

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode 22
        versionName '2.7.2'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}


dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.slf4j:slf4j-android:1.7.21'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
}

group="com.github.OOOOO-R-D"
version = '1.0'


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task classesJar(type: Jar) {
    from "$buildDir/intermediates/classes/release"
}

artifacts {
    archives classesJar
    archives javadocJar
    archives sourcesJar
}

project.afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "AndroidVideoCache"
                url = uri("https://maven.pkg.github.com/OOOOO-R-D/AndroidVideoCache")
                credentials {
                    username = githubProperties.getProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = githubProperties.getProperty("gpr.key") ?: System.getenv("TOKEN")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.ooooo.module'
                artifactId = 'videocache'
                version = "1.0.1"
            }
//            gpr(MavenPublication) {
//                from(components.release)
//            }
        }
    }
}
